apply plugin: 'java'

repositories {
    mavenCentral()
    maven {
        url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
    }
}

dependencies {
    compileOnly project(':mucommander-commons-file')
    compileOnly project(':mucommander-commons-conf')
    compileOnly project(':mucommander-commons-collections')
    compileOnly project(':mucommander-commons-io')
    compileOnly project(':mucommander-archiver')
    compileOnly project(':mucommander-command')
    compileOnly project(':mucommander-encoding')
    compileOnly project(':mucommander-preferences')
    compileOnly project(':mucommander-process')
    compileOnly project(':mucommander-translator')
    compileOnly project(':mucommander-protocol-api')
    compileOnly project(':mucommander-os-api')
    compileOnly project(':mucommander-viewer-api')

    compileOnly 'com.formdev:flatlaf:2.2'
    compileOnly 'org.violetlib:vaqua:10'

    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.jmdns:jmdns:3.5.5'
    implementation 'commons-collections:commons-collections:3.2.2'

    comprise 'org.unix4j:unix4j-command:0.6'

    // The following libs are required by JediTerm.
    // Since, I haven't found any repo where they are published,
    // so they have been built from sources (https://github.com/JetBrains/jediterm, c21a2b27629dd3760c603edeb258f9a862e75d34)
    // and placed directly into muC.
    comprise files('libs/jediterm-2.69.jar')
    comprise files('libs/eawtstub.jar')
    implementation 'com.google.guava:guava:31.1-jre'
    comprise 'org.jetbrains.jediterm:jediterm-typeahead:2.69'
    comprise 'org.jetbrains.kotlin:kotlin-stdlib-common:1.7.10'
    implementation 'org.jetbrains:annotations:23.0.0'
    comprise 'net.java.dev.jna:jna:5.10.0'
    comprise 'org.jetbrains.pty4j:pty4j:0.12.3'
    comprise 'net.java.dev.jna:jna-platform:5.10.0'
    comprise 'org.jetbrains.pty4j:purejavacomm:0.0.11.1'
    runtimeOnly 'org.jetbrains.intellij.deps:trove4j:1.0.20200330'
    comprise 'org.jetbrains.jediterm:jediterm-pty:2.69'
    comprise 'org.jetbrains.kotlin:kotlin-stdlib:1.7.10'

    testImplementation 'org.testng:testng:6.11'
    testImplementation 'junit:junit:4.12'
    testImplementation project(':mucommander-commons-file')
    testImplementation project(':mucommander-commons-conf')
    testImplementation project(':mucommander-commons-collections')
    testImplementation project(':mucommander-commons-io')
}

jar {
   from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'org/unix4j/**'
        include 'com/jediterm/**'
        include 'com/intellij/**'
        include 'com/pty4j/**'
        include 'gnu/trove/**'
        include 'jtermios/**'
        include 'purejavacomm/**'
        include 'kotlin/**'
        include 'org/jetbrains/**'
        include 'resources/com/pty4j/**'
        include 'com/sun/**'
        include 'org/apache/log4j/**'
        exclude 'codegen/**'
        exclude 'META-INF/**'
   } }
   bnd ('Bundle-Name': 'muCommander-core',
        'Bundle-Vendor': 'muCommander',
        'Bundle-Description': 'The core part of muCommander',
        'Bundle-DocURL': 'https://www.mucommander.com',
        'Specification-Title': "muCommander",
        'Specification-Vendor': "Arik Hadas",
        'Specification-Version': project.version,
        'Implementation-Title': "muCommander",
        'Implementation-Vendor': "Arik Hadas",
        'Implementation-Version': revision.substring(0, 7),
        'Build-Date': new Date().format('yyyyMMdd'),
        'Build-Url': "https://www.mucommander.com/version/nightly.xml",
        'Import-Package': 'org.violetlib.aqua;resolution:=dynamic,com.formdev.flatlaf;resolution:=dynamic,kotlin.internal.*;resolution:=dynamic,javax.jms;resolution:=dynamic,com.ibm.uvm.tools;resolution:=dynamic,sun.swing;resolution:=dynamic,org.apache.log4j;resolution:=dynamic,java.awt.*;resolution:=dynamic,sun.awt.*;resolution:=dynamic,kotlin.reflect.jvm.internal;resolution:=dynamic,com.sun.*;resolution:=dynamic,apple.awt.*;resolution:=dynamic,com.apple.*;resolution:=dynamic,sun.security.action;resolution:=dynamic,*',
        'Export-Package':
            'com.mucommander.core.desktop,' +
            'com.mucommander.job,' +
            'com.mucommander.job.impl,' +
            'com.mucommander.process,' +
            'com.mucommander.text,' +
            'com.mucommander.osgi,' +
            'com.mucommander.ui.dialog,' +
            'com.mucommander.ui.dialog.file,' +
            'com.mucommander.ui.dialog.server,' +
            'com.mucommander.ui.encoding,' +
            'com.mucommander.ui.theme,' +
            'com.mucommander.snapshot,' +
            'com.mucommander.ui.viewer,' +
            'com.mucommander.ui.main,' +
            'com.mucommander.ui.main.table',
        'Bundle-Activator': 'com.mucommander.Activator')
}
