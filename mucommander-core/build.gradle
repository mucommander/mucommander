apply plugin: 'java'

repositories {
    mavenCentral()
    maven {
        url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
    }
}

dependencies {
    compileOnly project(':mucommander-commons-file')
    compileOnly project(':mucommander-commons-conf')
    compileOnly project(':mucommander-commons-collections')
    compileOnly project(':mucommander-commons-io')
    compileOnly project(':mucommander-archiver')
    compileOnly project(':mucommander-command')
    compileOnly project(':mucommander-encoding')
    compileOnly project(':mucommander-preferences')
    compileOnly project(':mucommander-process')
    compileOnly project(':mucommander-translator')
    compileOnly project(':mucommander-protocol-api')
    compileOnly project(':mucommander-os-api')
    compileOnly project(':mucommander-viewer-api')
    compileOnly project(':mucommander-core-preload')


    compileOnly 'com.formdev:flatlaf:2.2'
    compileOnly 'org.violetlib:vaqua:10'

    // JediTerm deps: if you change any version below, remember to change it also in jetbrains-jediterm
    compileOnly 'org.jetbrains.jediterm:jediterm-core:3.57'
    compileOnly 'org.jetbrains.jediterm:jediterm-ui:3.57'
    compileOnly 'org.jetbrains.pty4j:pty4j:0.13.11'
    compileOnly 'org.jetbrains:annotations:26.0.2-1'
    // JediTerm deps end

    implementation 'ch.qos.logback:logback-core:1.2.13'
    implementation 'ch.qos.logback:logback-classic:1.2.13'
    implementation 'org.apache.commons:commons-collections4:4.4'

    comprise 'org.unix4j:unix4j-command:0.6'

    testImplementation 'org.testng:testng:7.11.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation project(':mucommander-commons-file')
    testImplementation project(':mucommander-commons-conf')
    testImplementation project(':mucommander-commons-collections')
    testImplementation project(':mucommander-commons-io')
}

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'org/unix4j/**'
        exclude 'codegen/**'
        exclude 'META-INF/**'
    } }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bundle {
        bnd ('Bundle-Name': 'muCommander-core',
                'Bundle-Vendor': 'muCommander',
                'Bundle-Description': 'The core part of muCommander',
                'Bundle-DocURL': 'https://www.mucommander.com',
                'Specification-Title': "muCommander",
                'Specification-Vendor': "Arik Hadas",
                'Specification-Version': project.version,
                'Implementation-Title': "muCommander",
                'Implementation-Vendor': "Arik Hadas",
                'Implementation-Version': revision.substring(0, 7),
                'Build-Date': new Date().format('yyyyMMdd'),
                'Build-Url': "https://www.mucommander.com/version/nightly.xml",
                'Import-Package': 'org.violetlib.aqua;resolution:=dynamic,com.formdev.flatlaf;resolution:=dynamic,com.apple.*;resolution:=dynamic,sun.security.action;resolution:=dynamic,*',
                'Export-Package':
                        'com.mucommander.core.desktop,' +
                                'com.mucommander.job,' +
                                'com.mucommander.job.impl,' +
                                'com.mucommander.process,' +
                                'com.mucommander.osgi,' +
                                'com.mucommander.search,' +
                                'com.mucommander.ui.action,' +
                                'com.mucommander.ui.action.impl,' +
                                'com.mucommander.ui.dialog,' +
                                'com.mucommander.ui.dialog.file,' +
                                'com.mucommander.ui.dialog.server,' +
                                'com.mucommander.ui.encoding,' +
                                'com.mucommander.ui.icon,' +
                                'com.mucommander.ui.text,' +
                                'com.mucommander.ui.theme,' +
                                'com.mucommander.snapshot,' +
                                'com.mucommander.search,' +
                                'com.mucommander.text,' +
                                'com.mucommander.ui.icon,' +
                                'com.mucommander.ui.main,' +
                                'com.mucommander.ui.main.table,' +
                                'com.mucommander.ui.viewer',
                'Bundle-Activator': 'com.mucommander.Activator')
    }
}
