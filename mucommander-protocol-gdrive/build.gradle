dependencies {
    api project(':mucommander-commons-file')
    api project(':mucommander-protocol-api')
    api project(':mucommander-encoding')
    api project(':mucommander-translator')
    api project(':mucommander-core')

    comprise 'com.google.api-client:google-api-client:2.0.0'
    comprise 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    comprise 'com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0'

    testImplementation(platform('org.junit:junit-bom:5.14.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testImplementation 'org.testng:testng:7.10.2'
}

repositories.mavenCentral()

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        exclude 'META-INF/**'
    }}
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bundle {
        bnd ('Bundle-Name': 'muCommander-gdrive',
                'Bundle-Vendor': 'muCommander',
                'Bundle-Description': 'Plugin for Google-Drive',
                'Bundle-DocURL': 'https://www.mucommander.com',
                'Export-Package': 'com.mucommander.commons.protocol.gdrive,com.google.api.client.auth.oauth2',
                'Import-Package': '!org.apache.avalon.framework.logger,!org.apache.log,!org.apache.log4j,!com.google.appengine.api,!com.google.apphosting.api,!com.google.appengine.api.utils,!io.grpc.override,*',
                'Bundle-Activator': 'com.mucommander.commons.file.protocol.gdrive.Activator',
                'Specification-Title': "muCommander",
                'Specification-Vendor': "Arik Hadas",
                'Specification-Version': project.version,
                'Implementation-Title': "muCommander",
                'Implementation-Vendor': "Arik Hadas",
                'Implementation-Version': revision.substring(0, 7),
                'Build-Date': new Date().format('yyyyMMdd'),
                'Build-Url': "https://www.mucommander.com/version/nightly.xml")
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
