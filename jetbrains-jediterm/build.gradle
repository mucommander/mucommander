repositories {
    mavenCentral()
    maven {
        url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
    }
}

dependencies {
    // The following libs are required by JediTerm.
    implementation 'org.checkerframework:checker-qual:3.24.0'

    // JediTerm deps: if you change any version below, remember to change it also in mucommander-core
    comprise 'org.jetbrains.jediterm:jediterm-core:3.47'
    comprise 'org.jetbrains.jediterm:jediterm-ui:3.47'
    comprise 'org.jetbrains.pty4j:pty4j:0.13.2'
    comprise 'org.jetbrains.pty4j:purejavacomm:0.0.11.1'

    implementation 'org.jetbrains:annotations:26.0.1'
    runtimeOnly 'org.jetbrains.intellij.deps:trove4j:1.0.20200330'
    // JediTerm deps end

    implementation 'org.apache.logging.log4j:log4j-1.2-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
}

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'com/jediterm/**'
        include 'com/intellij/**'
        include 'com/pty4j/**'
        include 'gnu/trove/**'
        include 'jtermios/**'
        include 'purejavacomm/**'
        include 'org/jetbrains/**'
        include 'resources/com/pty4j/**'
        exclude 'codegen/**'
        exclude 'META-INF/**'
    } }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bundle {
        bnd ('Bundle-Name': 'jetbrains-jediterm',
                'Bundle-Vendor': 'muCommander',
                'Bundle-Description': 'JediTerm and its dependencies',
                'Bundle-DocURL': 'https://www.mucommander.com',
                'Specification-Title': "muCommander",
                'Specification-Vendor': "Arik Hadas",
                'Specification-Version': project.version,
                'Implementation-Title': "muCommander",
                'Implementation-Vendor': "Arik Hadas",
                'Implementation-Version': revision.substring(0, 7),
                'Build-Date': new Date().format('yyyyMMdd'),
                'Build-Url': "https://www.mucommander.com/version/nightly.xml",
                'Export-Package':
                        'com.jediterm.core,' +
                                'com.jediterm.core.util,' +
                                'com.jediterm.terminal,' +
                                'com.jediterm.terminal.ui,' +
                                'com.jediterm.terminal.ui.settings,' +
                                'com.jediterm.pty,' +
                                'org.jetbrains.annotations,' +
                                'com.pty4j')
    }
}
