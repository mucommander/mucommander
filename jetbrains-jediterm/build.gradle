repositories {
    mavenCentral()
    maven {
        url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
    }
}

dependencies {
    // The following libs are required by JediTerm.
    implementation 'org.checkerframework:checker-qual:3.24.0'

    comprise 'org.jetbrains.jediterm:jediterm-core:3.17'
    comprise 'org.jetbrains.jediterm:jediterm-core-pty:3.0'     // if you change version here, remember to change it also in mucommander-core
    comprise 'org.jetbrains.jediterm:jediterm-typeahead:2.69'
    comprise 'org.jetbrains.jediterm:jediterm-ui:3.17'          // if you change version here, remember to change it also in mucommander-core
    comprise 'org.jetbrains.pty4j:pty4j:0.12.3'
    comprise 'org.jetbrains.pty4j:purejavacomm:0.0.11.1'

    implementation 'org.jetbrains:annotations:23.0.0'
    runtimeOnly 'org.jetbrains.intellij.deps:trove4j:1.0.20200330'
    comprise 'org.jetbrains.kotlin:kotlin-stdlib:1.7.10'
    comprise 'org.jetbrains.kotlin:kotlin-stdlib-common:1.7.10'

    comprise 'com.google.guava:guava:31.1-jre'
    comprise 'net.java.dev.jna:jna:5.10.0'
    comprise 'net.java.dev.jna:jna-platform:5.10.0'

    implementation 'log4j:log4j:1.2.17'
}

jar {
   from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'com/jediterm/**'
        include 'com/intellij/**'
        include 'com/pty4j/**'
        include 'gnu/trove/**'
        include 'jtermios/**'
        include 'purejavacomm/**'
        include 'kotlin/**'
        include 'org/jetbrains/**'
        include 'resources/com/pty4j/**'
        include 'com/sun/**'
        include 'com/google/**'
        exclude 'codegen/**'
        exclude 'META-INF/**'
   } }
   duplicatesStrategy = DuplicatesStrategy.EXCLUDE
   bnd ('Bundle-Name': 'jetbrains-jediterm',
        'Bundle-Vendor': 'muCommander',
        'Bundle-Description': 'JediTerm and its dependencies',
        'Bundle-DocURL': 'https://www.mucommander.com',
        'Specification-Title': "muCommander",
        'Specification-Vendor': "Arik Hadas",
        'Specification-Version': project.version,
        'Implementation-Title': "muCommander",
        'Implementation-Vendor': "Arik Hadas",
        'Implementation-Version': revision.substring(0, 7),
        'Build-Date': new Date().format('yyyyMMdd'),
        'Build-Url': "https://www.mucommander.com/version/nightly.xml",
        'Import-Package': '!javax.annotation.*,!com.google.appengine*,!com.google.apphosting.*,!kotlin.internal.*,!com.ibm.uvm.tools,!kotlin.reflect.jvm.internal,*',
        'Export-Package':
            'com.jediterm.core,' +
            'com.jediterm.terminal,' +
            'com.jediterm.terminal.ui,' +
            'com.jediterm.terminal.ui.settings,' +
            'com.jediterm.pty,' +
            'org.jetbrains.annotations,' +
            'com.pty4j')
}
