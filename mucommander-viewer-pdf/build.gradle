dependencies {
    api project(':mucommander-core')
    api project(':mucommander-viewer-api')
    api project(':mucommander-translator')

    comprise 'com.github.pcorless.icepdf:icepdf-viewer:7.1.3'
    comprise 'com.twelvemonkeys.imageio:imageio-psd:3.11.0'
    comprise 'avalon-framework:avalon-framework-api:4.3'
    implementation 'org.jmdns:jmdns:3.5.5'

    // Use JUnit test framework
    testImplementation(platform('org.junit:junit-bom:5.14.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

repositories.mavenCentral()

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it) }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bundle {
        bnd ('Bundle-Name': 'muCommander-viewer-pdf',
                'Bundle-Vendor': 'muCommander',
                'Bundle-Description': 'Library for viewing PDF files',
                'Bundle-DocURL': 'https://www.mucommander.com',
                'Export-Package': 'com.mucommander.viewer.pdf',
                'Import-Package':
                        '!com.sun.*,' +
                                '!javax.media.*,' +
                                '!com.ibm.uvm.tools,' +
                                '!magick,' +
                                '!com.inet.jortho,' +
                                '!org.apache.log.*,' +
                                '*',
                'Bundle-Activator': 'com.mucommander.viewer.pdf.Activator',
                'Specification-Title': "muCommander",
                'Specification-Vendor': "Arik Hadas",
                'Specification-Version': project.version,
                'Impxlementation-Title': "muCommander",
                'Implementation-Vendor': "Arik Hadas",
                'Implementation-Version': revision.substring(0, 7),
                'Build-Date': new Date().format('yyyyMMdd'),
                'Build-Url': "https://www.mucommander.com/version/nightly.xml")
        bnd('-fixupmessages': '^Classes found in the wrong directory: .*')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
