dependencies {
    api project(':mucommander-core')
    api project(':mucommander-viewer-api')
    api project(':mucommander-translator')

    comprise 'com.github.pcorless.icepdf:icepdf-viewer:7.0.0'
    comprise 'log4j:log4j:1.2.17'
    comprise 'com.twelvemonkeys.imageio:imageio-psd:3.9.4'
    comprise 'avalon-framework:avalon-framework:4.1.5'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

repositories.mavenCentral()

jar {
   bnd('-fixupmessages': '^Classes found in the wrong directory: .*')
   from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it) }
   duplicatesStrategy = DuplicatesStrategy.EXCLUDE
   bnd ('Bundle-Name': 'muCommander-viewer-pdf',
        'Bundle-Vendor': 'muCommander',
        'Bundle-Description': 'Library for viewing PDF files',
        'Bundle-DocURL': 'https://www.mucommander.com',
        'Export-Package': 'com.mucommander.viewer.pdf',
        'Import-Package': 
          '!com.sun.*,' +
          '!javax.media.*,' +
          '!com.ibm.uvm.tools,' +
          '!magick,' +
          '!com.inet.jortho,' +
          '!org.apache.log.*,' +
          '*',
        'Bundle-Activator': 'com.mucommander.viewer.pdf.Activator',
        'Specification-Title': "muCommander",
        'Specification-Vendor': "Arik Hadas",
        'Specification-Version': project.version,
        'Impxlementation-Title': "muCommander",
        'Implementation-Vendor': "Arik Hadas",
        'Implementation-Version': revision.substring(0, 7),
        'Build-Date': new Date().format('yyyyMMdd'),
        'Build-Url': "https://www.mucommander.com/version/nightly.xml")
}

