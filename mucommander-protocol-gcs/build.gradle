repositories.mavenCentral()

dependencies {
    api project(':mucommander-commons-file')
    api project(':mucommander-protocol-api')
    api project(':mucommander-translator')

    comprise 'com.google.cloud:google-cloud-storage:2.16.0'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.testng:testng:7.7.0'
}

configurations {
    testImplementation.extendsFrom comprise
}

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'com/google/auth/**'
        include 'com/google/common/**'
        include 'com/google/cloud/**'
        include 'com/google/gson/**'
        include 'com/google/api/client/**'
        include 'com/google/api/client/**'
        include 'com/google/api/core/**'
        include 'com/google/api/gax/**'
        include 'com/google/api/services/storage/**'
        include 'org/threeten/bp/**'
        include 'io/opencensus/trace/**'
        include 'io/opencensus/common/**'
        include 'io/opencensus/internal/**'
        include 'io/opencensus/contrib/**'
        include 'io/grpc/*'
        include 'com/fasterxml/jackson/**'
        exclude 'META-INF/**'
    }}
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bnd('Bundle-Name': 'muCommander-gcs',
            'Bundle-Vendor': 'muCommander',
            'Bundle-Description': 'Plugin for Google-Cloud Storage',
            'Bundle-DocURL': 'https://www.mucommander.com',
            'Export-Package': 'com.mucommander.commons.file.protocol.gcs',
            'Import-Package': 'org.osgi.framework.*,com.mucommander.*,javax.swing.*,org.slf4j.*,javax.net.ssl.*',
            'Bundle-Activator': 'com.mucommander.commons.file.protocol.gcs.Activator',
            'Specification-Title': "muCommander",
            'Specification-Vendor': "Arik Hadas",
            'Specification-Version': project.version,
            'Implementation-Title': "muCommander",
            'Implementation-Vendor': "Miroslav Spak",
            'Implementation-Version': revision.substring(0, 7),
            'Build-Date': new Date().format('yyyyMMdd'),
            'Build-Url': "https://www.mucommander.com/version/nightly.xml")
}

test {
    useJUnit()
}
