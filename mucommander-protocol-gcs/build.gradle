repositories.mavenCentral()

dependencies {
    api project(':mucommander-commons-file')
    api project(':mucommander-protocol-api')
    api project(':mucommander-translator')

    comprise ('com.google.cloud:google-cloud-storage:2.23.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
    // the version of gson here needs to be in sync with that in the 'gson' project
    compileOnly 'com.google.code.gson:gson:2.11.0'

    testImplementation(platform('org.junit:junit-bom:5.14.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    testImplementation 'org.testng:testng:7.11.0'
}

configurations {
    testImplementation.extendsFrom comprise
}

jar {
    from configurations.comprise.collect { it.isDirectory() ? it : zipTree(it).matching {
        include 'com/google/auth/**'
        include 'com/google/common/**'
        include 'com/google/cloud/**'
        include 'com/google/api/client/**'
        include 'com/google/api/client/**'
        include 'com/google/api/core/**'
        include 'com/google/api/gax/**'
        include 'com/google/api/services/storage/**'
        include 'org/threeten/bp/**'
        include 'io/opencensus/trace/**'
        include 'io/opencensus/common/**'
        include 'io/opencensus/internal/**'
        include 'io/opencensus/contrib/**'
        include 'io/grpc/*'
        exclude 'META-INF/**'
    }}
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    bundle {
        bnd('Bundle-Name': 'muCommander-gcs',
                'Bundle-Vendor': 'muCommander',
                'Bundle-Description': 'Plugin for Google-Cloud Storage',
                'Bundle-DocURL': 'https://www.mucommander.com',
                'Export-Package': 'com.mucommander.commons.file.protocol.gcs',
                'Import-Package': 'org.osgi.framework.*,com.mucommander.*,javax.swing.*,org.slf4j.*,javax.net.ssl.*',
                'Bundle-Activator': 'com.mucommander.commons.file.protocol.gcs.Activator',
                'Specification-Title': "muCommander",
                'Specification-Vendor': "Arik Hadas",
                'Specification-Version': project.version,
                'Implementation-Title': "muCommander",
                'Implementation-Vendor': "Miroslav Spak",
                'Implementation-Version': revision.substring(0, 7),
                'Build-Date': new Date().format('yyyyMMdd'),
                'Build-Url': "https://www.mucommander.com/version/nightly.xml")
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
